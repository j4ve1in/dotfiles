#!/bin/zsh

typeset -g D{ROOT,USER,REPO,AUTHOR,LICENSE}
: ${DROOT:=~/.files} ${DUSER:=ytet5uy4}
: ${DREPO:=https://github.com/${DUSER}/dotfiles}
: ${DAUTHOR:=${DUSER}} ${DLICENSE:=MIT}

typeset -grA fg=(
  main      "${DMAINCOLOR:=75}"
  sub       "${DSUBCOLOR:=240}"
  success   "${DSUCCESSCOLOR:=32}"
  error     "${DERRORCOLOR:=129}"
  underline '\e[4;39;49m'
  reset     '\e[0;39;49m'
)

typeset -gA opt
while (( $# > 0 )); do
  case "$1" in
    -*)
      [[ "$1" =~ 'f' ]] && opt[full]='1'
      [[ "$1" =~ 'y' ]] && opt[yes]='1'
      [[ "$1" =~ 'p' ]] && opt[plugin]='1'
      [[ "$1" =~ 'P' ]] && opt[provision]='1'
      shift
      ;;
    *) shift ;;
  esac
done

main() {
  (( $opt[plugin] )) && install_plugin && exit 0
  print_header
  install
}

print_header() {
  clear
  printf  "\n\e[1;38;5;$fg[main];49m"
  echo    '          __        __   ____ __ __              '
  echo    '     ____/ /____   / /_ / __//_// /___   _____   '
  echo    '    / __  // __ \ / __// /_ / // // _ \ / ___/   '
  echo    '   / /_/ // /_/ // /_ / __// // //  __//__  /    '
  echo    '   \__,_/ \____/ \__//_/  /_//_/ \___//____/     '
  printf  "$fg[reset]\n"

  cprint  "           $DREPO   \n" "$fg[sub]"
  cprintf '   Author: '            "$fg[main]"; printf "$DAUTHOR"
  cprintf '   License: '           "$fg[main]"; echo   "$DLICENSE"
  cprintf '   Provisioning: '      "$fg[main]"
  (( $opt[provision] )) && cprint 'enable' "$fg[main]" || print 'disable'
  cprintf '   Full Installation: ' "$fg[main]"
  (( $opt[full] )) && cprint 'enable' "$fg[main]" || print 'disable'
  cprintf '   Option Assume yes: ' "$fg[main]"
  (( $opt[yes] )) && cprint 'enable' "$fg[main]" || print 'disable'
  echo

  ! (( $opt[yes] )) && warning
}

warning() {
  echo '   If the file exists, it will be ruthlessly clobbered'
  printf '   Are you sure you want to continue (yes/no)? '
  read -q ANSWER; echo "\n"
  case $ANSWER in
    'Y' | 'y' ) install ;;
    * ) exit 0 ;;
  esac
}

install() {
  # Provisioning
  (( $opt[provision] )) && provisioning

  # Download
  download

  path=($path $DROOT/bin(N-/))

  # Backup and Deploy
  dbackup && ddeploy

  # Install plugin
  (( $opt[full] )) && install_plugin

  # Restart
  (( $opt[yes] )) && restart || restart_message
}

provisioning() {
  typeset ANSIBLE_DREPO tempdir
  ANSIBLE_DREPO='https://github.com/${DUSER}/ansible-dotfiles/tarball/master'
  tempdir=`mktemp -d /tmp/dotfiles-XXXXXX`

  underline "Provision for installing dotfiles"
  echo ' Download playbook of ansible for dotfiles'
  curl -sL $ANSIBLE_DREPO | tar xzC $tempdir --strip=1
  cprintf '  Repository: ' "$fg[main]"
  echo "$ANSIBLE_DREPO"
  cprintf '  Directory: ' "$fg[main]"
  echo $tempdir
  lprintf ' Download playbook of ansible for dotfiles' "ansible-playbook $tempdir/site.yml"
  echo
}

download() {
  underline "Download dotfiles"
  if lprintf ' Checking git command' 'type git'; then
    cprintf '  Use: ' "$fg[main]"
    type -a git | tail -n 1 | awk '{print $3}'
  else
    echo ' Please install git or update your path to run git command'
    exit 1
  fi

  lprintf ' Downloading dotfiles' "git clone --depth 1 $DREPO $DROOT"
  cprintf '  Repository: ' "$fg[main]"
  echo "$DREPO"
  cprintf '  Directory: ' "$fg[main]"
  echo "$DROOT"
  echo
}

restart_message() {
  printf "Do you want to restart shell (yes/no)? "
  read -q ANSWER; echo
  case $ANSWER in
    "Y" | "y" ) restart ;;
    * ) exit 0 ;;
  esac
}

restart() { clear; exec $SHELL -l; }

install_plugin() {
  underline "Install plugin"

  typeset -ar NAME=('Dein' 'Zplug' 'TPM')

  lprintf " Downloading ${NAME[*]}" 'download_plugin'
  echo
}

download_plugin() {
  typeset -ar REPO=('Shougo/dein.vim' 'zplug/zplug' 'tmux-plugins/tpm')
  typeset -r GITHUB_URL='https://github.com/'
  typeset -ar DIR=(
    "$HOME/.vim/bundle/repos/github.com/Shougo/dein.vim"
    "$HOME/.zsh/bundle/zplug"
    "$HOME/.tmux/bundle/tpm"
  )
  paste -d ' ' <(printf "%s\n" "${REPO[@]}") <(printf "%s\n" "${DIR[@]}") \
    | sed -e "s|^|${GITHUB_URL}|g" \
    | xargs -L 1 -n 2 -P "${#REPO[*]}" git clone >/dev/null 2>&1
}

print() { printf "$@\n"; }

cprint() { print "%b%b%b" "\e[1;38;5;${2};49m" "$1" "$fg[reset]"; }

cprintf() { printf "%b%b%b" "\e[1;38;5;${2};49m" "$1" "$fg[reset]"; }

underline() { print "%b%b%b" "$fg[underline]" "$1" "$fg[reset]"; }

lprintf() {
  typeset -r MSG="$1" CMD="${@:2}"
  ${=CMD} >/dev/null 2>&1 &
  while :; do
    jobs %% > /dev/null 2>&1 || break
    printf "\r%s%b%s${fg[reset]}" "${MSG}" "\e[1;38;5;${fg[main]};49m" ".  "
    sleep 0.7
    jobs %% > /dev/null 2>&1 || break
    printf "\r%s%s%b%s${fg[reset]}" "${MSG}" "\e[1;38;5;${fg[main]};49m" "." ". "
    sleep 0.7
    jobs %% > /dev/null 2>&1 || break
    printf "\r%s%s%b%s${fg[reset]}" "${MSG}" "\e[1;38;5;${fg[main]};49m" ".." "."
    sleep 0.7
  done
  if wait $!; then
    printf "\r${MSG}..."
    cprint 'done' "$fg[success]"
    return 0
  else
    printf "\r${MSG}..."
    cprint 'error' "$fg[error]"
    return 1
  fi
}

main
